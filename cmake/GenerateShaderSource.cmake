find_program(DXC_EXECUTABLE dxc
        PATHS "$ENV{VULKAN_SDK}/Bin"
        REQUIRED
)

function(add_shader_embedding_target TARGET_NAME INPUT_DIR OUTPUT_FILE)
    file(GLOB SHADER_FILES CONFIGURE_DEPENDS "${INPUT_DIR}/*.hlsl")

    set(HEADER_CONTENT "// Generated by CMake\nexport module Render.GeneratedShaders;\nimport std;\n\nnamespace Engine::GeneratedShaders {\n")

    set(INTERMEDIATE_DIR "${CMAKE_CURRENT_BINARY_DIR}/_shader_int_${TARGET_NAME}")
    file(MAKE_DIRECTORY "${INTERMEDIATE_DIR}")

    foreach (SHADER_PATH ${SHADER_FILES})
        get_filename_component(FILE_NAME ${SHADER_PATH} NAME)

        string(REPLACE "." "_" SAFE_VAR_NAME "${FILE_NAME}")
        string(REPLACE "_hlsl" "" SAFE_VAR_NAME "${SAFE_VAR_NAME}")

        if (FILE_NAME MATCHES ".*\\.vs\\.hlsl$")
            set(PROFILE "vs_6_0")
        elseif (FILE_NAME MATCHES ".*\\.ps\\.hlsl$")
            set(PROFILE "ps_6_0")
        else ()
            continue()
        endif ()

        set(SPV_FILE "${INTERMEDIATE_DIR}/${FILE_NAME}.spv")

        execute_process(
                COMMAND ${DXC_EXECUTABLE} -T ${PROFILE} -E main -spirv -Fo "${SPV_FILE}" "${SHADER_PATH}"
                RESULT_VARIABLE DXC_RESULT
                ERROR_VARIABLE DXC_ERROR
        )

        if (NOT DXC_RESULT EQUAL 0)
            message(FATAL_ERROR "DXC Compile Error for ${FILE_NAME}: ${DXC_ERROR}")
        endif ()

        file(READ "${SPV_FILE}" HEX_CONTENT HEX)
        file(SIZE "${SPV_FILE}" BIN_SIZE)
        string(REGEX REPLACE "([0-9a-f][0-9a-f])" "0x\\1, " HEX_LIST "${HEX_CONTENT}")

        string(APPEND HEADER_CONTENT "    export constexpr std::array<std::uint8_t, ${BIN_SIZE}> ${SAFE_VAR_NAME} = { ${HEX_LIST} };\n")
    endforeach ()

    string(APPEND HEADER_CONTENT "}\n")
    file(WRITE "${OUTPUT_FILE}" "${HEADER_CONTENT}")

    target_sources(${PROJECT_NAME}
            PRIVATE
            FILE_SET cxx_modules
            TYPE CXX_MODULES FILES
            "${OUTPUT_FILE}"
    )
endfunction()